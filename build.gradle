plugins {
	id "java"
	id "jacoco"
	id 'maven-publish'
	id 'com.github.kt3k.coveralls' version '2.6.3'
}

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

group = 'com.github.markrileybot.heatshrink'
version = '0.4.0'

dependencies {
	testImplementation 'junit:junit:4.12'
	testImplementation 'commons-io:commons-io:2.5'
	testImplementation files(sourceSets.test.resources.srcDirs)
}

jacocoTestReport {
	reports {
		xml.enabled = true // coveralls plugin depends on xml format report
		html.enabled = true
	}
}


task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
	from javadoc.destinationDir
}

task exeJar(type: Jar, dependsOn: 'classes') {
	from sourceSets.main.output
	manifest {
		attributes 'Main-Class': 'heatshrink.Main'
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifact sourceJar {
				classifier 'sources'
			}

			artifact javadocJar {
				classifier 'javadoc'
			}

			artifact exeJar {
				classifier 'exe'
			}
		}
	}
}

