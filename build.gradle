plugins {
	id "java"
	id "jacoco"
	id 'maven-publish'
	id 'com.github.kt3k.coveralls' version '2.12.0'
	id 'org.danilopianini.publish-on-central' version '1.0.2'
}

repositories {
	mavenLocal()
	mavenCentral()
}

group = 'io.github.markrileybot'
version = '1.0.1'

dependencies {
	testImplementation 'junit:junit:4.12'
	testImplementation 'commons-io:commons-io:2.5'
	testImplementation files(sourceSets.test.resources.srcDirs)
}

jacocoTestReport {
	reports {
		xml.enabled = true // coveralls plugin depends on xml format report
		html.enabled = true
	}
}


task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

task exeJar(type: Jar, dependsOn: 'classes') {
	from sourceSets.main.output
	manifest {
		attributes 'Main-Class': 'heatshrink.Main'
	}
}

publishOnCentral {
	if (project.version.endsWith("-SNAPSHOT")) {
		mavenCentralSnapshotsRepository()
	}

	projectDescription.set(file('README.md').text)
	projectLongName.set(project.name)
	licenseName.set("Apache License, Version 2.0")
	licenseUrl.set("http://www.apache.org/licenses/LICENSE-2.0")
	projectUrl.set("https://github.com/markrileybot/${project.name}")
	scmConnection.set("git:git@github.com:markrileybot/${project.name}")
}

signing {
	useGpgCmd()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			pom {
				developers {
					developer {
						name.set("Mark Riley")
						email.set("mark@markriley.net")
						url.set("https://github.com/markrileybot/${project.name}")
					}
				}
			}

			from components.java

			artifact sourceJar {
				classifier 'sources'
			}

			artifact javadocJar {
				classifier 'javadoc'
			}

			artifact exeJar {
				classifier 'exe'
			}
		}
	}
}

